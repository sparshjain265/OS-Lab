alias functionNum R1;
alias currentPID R2;

//free user area page
if(functionNum == 2) then
	//get page number
	alias page R3;
	page = [PROCESS_TABLE + 16*currentPID + 11];
	//Call release page
	multipush(R1, R2, R3);
	R1 = 2;
	R2 = page;
	call MOD_2;
	multipop(R1, R2, R3);
	return;
endif;

//Exit process
if(functionNum == 3) then
	//call free page table
	multipush(R1, R2);
	R1 = 4;
	R2 = currentPID;
	call MOD_1;
	multipop(R1, R2);

	//call free user area page
	multipush(R1, R2);
	R1 = 2;
	R2 = currentPID;
	call MOD_1;
	multipop(R1, R2);

	//Set state to TERMINATED
	[PROCESS_TABLE + 16*currentPID + 4] = TERMINATED;
	
endif;

//Free Page Table
if(functionNum == 4) then
	//Invalidate Library Pages
	[PTBR + 0] = -1;
	[PTBR + 1] = "0000";
	[PTBR + 2] = -1;
	[PTBR + 3] = "0000";

	//Release valid Pages
	alias i R3;
	i = 2;
	while(i < 10) do
		alias aux R4;
		aux = [PTBR + 2*i + 1];
		if(aux == "0100" || aux == "0110") then
			multipush(R1, R2, R3, R4);
			R1 = 2;
			R2 = [PTBR + 2*i];
			call MOD_2;
			multipop(R1, R2, R3, R4);
		endif;
		i = i + 1;
	endwhile;
	
	alias count R10;
	count = 0;
	alias block R5;
	while(count < 10) do
		block = [DISK_MAP_TABLE + currentPID*10 + count];

		//release disk blocks for heap and stack (if valid)
		if((count == 2 || count == 3 || count == 8 || count == 9) && block != -1) then
			multipush(R1, R2, R3, R4, R5, R10);
			R1 = 4;
			R2 = block;
			R3 = currentPID;
			call MOD_2;
			multipop(R1, R2, R3, R4, R5, R10);
		endif;

		//invalidate disk map entries
		[DISK_MAP_TABLE + currentPID*10 + count] = -1;

		count = count + 1;
	endwhile;

endif;

return;