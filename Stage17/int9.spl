//Set Mode Flag in Process Table
[PROCESS_TABLE + [SYSTEM_STATUS_TABLE]*16 + 9] = 9;

alias userSP R0;
userSP=SP;

[PROCESS_TABLE + ([SYSTEM_STATUS_TABLE + 1]*16 + 13)] = SP; 			//Storing the user stack pointter
SP = [PROCESS_TABLE + ([SYSTEM_STATUS_TABLE + 1]*16 + 11)]*512 - 1;	//Set SP to beginning of kernel stack

alias fileName R1;																	//Get fileName from user stack
fileName = [[PTBR + 2*((userSP - 2)/512)]*512 + (userSP - 2)%512];

//search for file 
alias i R2;
i = 0;
while (i < 60) do
	if(fileName == [INODE_TABLE + i*16 + 1] && [INODE_TABLE + i*16] == EXEC) then
		break;
	endif;
	i = i + 1;
endwhile;

//check if found
if(i == 60) then
	//return -1
	alias retAdd R3;
	retAdd = ([PTBR + 2*((userSP - 1)/512)]*512 + (userSP - 1)%512);
	[retAdd] = -1;
	//Set Mode Flag in Process Table, set SP, and return to user
	[PROCESS_TABLE + [SYSTEM_STATUS_TABLE]*16 + 9] = 0;
	SP = userSP;
	ireturn;
endif;

//call exit process
multipush(R0, R1, R2, R3);
R1 = 3;
R2 = [SYSTEM_STATUS_TABLE + 1];
call MOD_1;
multipop(R0, R1, R2, R3);

//Get user area page number
alias userArea R4;
userArea = [PROCESS_TABLE + ([SYSTEM_STATUS_TABLE + 1]*16 + 11)];

//Reclaim the page
[MEMORY_FREE_LIST + userArea] = [MEMORY_FREE_LIST + userArea] + 1;
[SYSTEM_STATUS_TABLE + 2] = [SYSTEM_STATUS_TABLE + 2] - 1;

//Set SP for the new process
SP = userArea*512 - 1;

//Set the state to running
[PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1]*16 + 4] = RUNNING;
//Set inode index
[PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1]*16 + 7] = i;

//Set Page Table Entries
[PTBR + 0] = 63;		//Library
[PTBR + 1] = "0100";
[PTBR + 2] = 64;
[PTBR + 3] = "0100";

//Get free pages and allocate 2 heap and 2 stack pages
multipush(R0, R1, R2, R3, R4);
R1 = 1;
R2 = [SYSTEM_STATUS_TABLE + 1];
call MOD_2;

[PTBR + 4] = R0;		//Heap1
[PTBR + 5] = "0110";

R1 = 1;
R2 = [SYSTEM_STATUS_TABLE + 1];
call MOD_2;

[PTBR + 6] = R0;		//Heap2
[PTBR + 7] = "0110";

R1 = 1;
R2 = [SYSTEM_STATUS_TABLE + 1];
call MOD_2;

[PTBR + 16] = R0;		//Stack1
[PTBR + 17] = "0110";

R1 = 1;
R2 = [SYSTEM_STATUS_TABLE + 1];
call MOD_2;

[PTBR + 18] = R0;		//Stack2
[PTBR + 19] = "0110";

multipop(R0, R1, R2, R3, R4);

//Get number of pages
alias numberOfPages R5;
numberOfPages = [INODE_TABLE + i*16 + 2]/512;
if([INODE_TABLE + i*16 + 2]%512 > 0) then
	numberOfPages = numberOfPages + 1;
endif;

//Allocate code pages
alias j R6;
j = 0;
while(j < numberOfPages) do
	multipush(R0, R1, R2, R3, R4, R5, R6);
	R1 = 1;
	R2 = [SYSTEM_STATUS_TABLE + 1];
	call MOD_2;
	R7 = R0;
	multipop(R0, R1, R2, R3, R4, R5, R6);
	[PTBR + 8 + j*2] = R7;
	[PTBR + 8 + j*2 + 1] = "0100";
	loadi(R7, [INODE_TABLE + i*16 + 8 + j]);	//load code blocks
	j = j + 1;
endwhile;

while(j < 4) do
	[PTBR + 8 + j*2] = -1;
	[PTBR + 8 + j*2 + 1] = "0000";
	j = j + 1;
endwhile;

//Store the entry point and change stack pointer
[[PTBR + 16]*512] = [[PTBR + 8]*512 + 1];
SP = 8*512;

//change Mode and return to user process
[PROCESS_TABLE + [SYSTEM_STATUS_TABLE]*16 + 9] = 0;
ireturn;
